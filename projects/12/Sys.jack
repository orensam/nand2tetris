// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library of basic system services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() 
    {
    	do Memory.init();    	
    	do Math.init();    	
		do Screen.init();    	
    	do Output.init();
    	do Keyboard.init();
    	do Main.main();
    	do Sys.halt();
        return;
    }

    /** Halts execution. */
    function void halt() 
    {
        while(true)
        {
        }
        return;
    }

    /** Waits approximately duration milliseconds and then returns. */
    function void wait(int duration) 
    {
        var int i, j, k;
        
        if (duration < 0)
        {
            do Sys.error(1);
            return;
        }
        
        let j = 0;
        while (j < (duration))
        {
            let j = j + 1;
            let k = 0;
            while (k < 3)
            {
                let i = 100 * 100;
                let k = k + 1;
            }
        }
        return;
    }

    /** Prints the given error code in the form "ERR<errorCode>", and halts. */
    function void error(int errorCode) 
    {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}

